d10p1:{
    a:{{ssr[;;""]/[x;("[[]]";"()";"{}";"<>")]}/[x]}each x;
    sum(")]}>"!3 57 1197 25137)(first each a inter\:")]}>")except" "};
d10p2:{
    a:{{ssr[;;""]/[x;("[[]]";"()";"{}";"<>")]}/[x]}each x;
    b:a where not any each ")]}>" in/:a;
    med 5 sv/:("([{<"!1+til 4)reverse each b};

/
x:();
x,:enlist"[({(<(())[]>[[{[]{<()<>>";
x,:enlist"[(()[<>])]({[<{<<[]>>(";
x,:enlist"{([(<{}[<>[]}>{[]{[(<()>";
x,:enlist"(((({<>}<{<{<>}{[]{[]{}";
x,:enlist"[[<[([]))<([[{}[[()]]]";
x,:enlist"[{[{({}]{}}([{[{{{}}([]";
x,:enlist"{<[[]]>}<{[{[{[]{()[[[]";
x,:enlist"[<(<(<(<{}))><([]([]()";
x,:enlist"<{([([[(<>()){}]>(<<{{";
x,:enlist"<{([{{}}[<[[[<>{}]]]>[]]";

d10p1 x //26397
d10p2 x //288957
